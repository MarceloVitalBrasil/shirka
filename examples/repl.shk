-- Copyright (c) 2013, Jeremy Pinat.

------------------------------------------------------------------------------
--                                                                          --
--                                   REPL                                   --
--                                                                          --
-- A rudimentary REPL for Shirka. Reserves do not persist between issued    --
-- commands.                                                                --
------------------------------------------------------------------------------

-- CONSTANTS

"> " -> PROMPT

-- OPERATIONS

(=> ask-code)
  [ PROMPT print gets $parse
    >> :$parse/failed =
    (if)
      [ [ << << "Syntax error!" puts [] ]
        [                            ] ] ]

(=> exit-cmd?)
  [ length 1 =
    (if)
      [ [      >> uncons >< <<
          [/exit] uncons >< <<
          =
          (if)
            [ [ << TRUE  ]
              [    FALSE ] ] ]
        [ FALSE ] ] ]

-- MAIN

"Interactive Shirka"        puts
"Enter `/exit' to leave.\n" puts

  :$repl/continue
([:$repl/continue =] while)
  [ ask-code
    exit-cmd?
    (if)
      [ [   :$repl/exit     ]
        [ ! :$repl/continue ] ] ]
